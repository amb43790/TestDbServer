#!/usr/bin/env perl

use strict;
use warnings;

use TestDbServer::CmdLine qw(get_user_agent url_for assert_success get_template_name_from_id foreach_database_or_template);
use JSON qw(decode_json);

if (grep { $_ eq '--short-help'} @ARGV) {
    print_short_help();
    exit 0;
}

my $entity = shift @ARGV;
unless ($entity eq 'templates'
    or
    $entity eq 'databases'
) {
    print STDERR "Cannot list $entity\n";
    exit 1;
}

foreach_database_or_template($entity, make_printer_for_each_entity($entity));

sub make_printer_for_each_entity {
    my $type = shift;

    my %entity_data_formatter = (
        'templates' => \&_get_template_data,
        'databases' => \&_get_database_data,
    );

    my $formatter = $entity_data_formatter{$type};
    return sub {
        print join("\t", $formatter->(@_)), "\n";
    };
}

sub _get_template_data {
    my($data) = @_;
    return @$data{'name','note'};
}

sub _get_database_data {
    my($data) = @_;
    my $template_name = get_template_name_from_id($data->{template_id});
    unless (defined $template_name) {
        $template_name = '<none>';
    }

    return ($data->{name},
            "tmpl: $template_name",
            'expires: ' . $data->{expires});
}

sub print_short_help {
    print "list templates or databases";
}
